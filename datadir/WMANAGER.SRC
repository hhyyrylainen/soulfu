#define button_one    self.stat00l
#define button_two    self.stat00h
#define button_three  self.stat01l
#define button_four   self.stat01h
#define button_five   self.stat02l
#define button_six    self.stat02h
#define button_seven  self.stat03l
#define button_eight  self.stat03h
#define button_nine   self.stat04l
#define file_slider   self.stat04h
#define filename      self.string0

//-----------------------------------------------------------------------
Spawn()
  int self
  self = FindSelf()
  button_one = 0
  button_two = 0
  button_three = 0
  button_four = 0
  button_five = 0
  button_six = 0
  button_seven = 0
  button_eight = 0
  button_nine = 0
  file_slider = 0
  StringClear(filename)


//-----------------------------------------------------------------------
Refresh()
  int    i, self, first_file, click, subwin
  float screenx

  // Allow us to save data in the window's properties...
  self = FindSelf()
  screenx = SystemGet(SYS_SCREENPARAMS, 0, 2)


  // Button actions
  if(button_one == 250)
    ButtonSound()
    FileOpen(filename, FILE_IMPORT)
  if(button_two == 250)
    ButtonSound()
    FileOpen(filename, FILE_EXPORT)
  if(button_three == 250)
    ButtonSound()
    FileOpen(filename, FILE_DELETE)
  if(button_four == 250)
    ButtonSound()
    UpdateFiles(UPDATE_END)
  if(button_five == 250)
    ButtonSound()
    UpdateFiles(UPDATE_SDFSAVE)
  if(button_six == 250)
    ButtonSound()
    subwin = Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WEMACS.RUN")
    if(subwin)
      StringClear(subwin.string0)
      StringAppend(subwin.string0, filename, 16)
      subwin.stat04h = 255
  if(button_seven == 250)
    ButtonSound()
    subwin = Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WMODELER.RUN")
    if(subwin)
      StringClear(subwin.string0)
      StringAppend(subwin.string0, filename, 16)
      i = StringLength(subwin.string0)
      while(i > 0)
        if(StringGetValue(subwin.string0, i-1) == '.')
          i = 0
        else
          StringChopRight(subwin.string0, 1)
          i--
      StringAppend(subwin.string0, "RDY", 16)
  if(button_eight == 250)
    ButtonSound()
    subwin = Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WROOMER.RUN")
    if(subwin)
      StringClear(subwin.string0)
      StringAppend(subwin.string0, filename, 16)
  if(button_nine == 250)
    ButtonSound()
    GoPoof(POOF_SELF)



  // Title bar
  TitleBar(TRUE, 0.0,-10.0, "File Util")


  // Legend window
  WindowBorder(FALSE, 6.0, 8.75, 12,  1, BORDER_LOW_TITLE)
  WindowImage(1.0, 0.2, 1.0, 1.0, "FILE:WINYABOX.RGB", NULL, 0)
  WindowString(WHITE, 3.0,  0.2, "= No FTP")


  // Selection window
  WindowBorder(FALSE, -18.0, 8.75, 22.5,  2, BORDER_LOW_TITLE)
  WindowString(WHITE, 1.0,  0.2, "File:")
  WindowInput(6.0, 0.2, 13, filename, 1)
  StringUppercase(filename)
  StringClear(String(0))
  StringAppendNumber(String(0), SystemGet(SYS_FILEFREE, 0, 0), 256)
  StringAppend(String(0), " open slots", 256)
  WindowString(WHITE, 1.0,  1.2, String(0))
  if(CheckExtension(filename, "RGB") || CheckExtension(filename, "JPG") || CheckExtension(filename, "PCX"))
    StringClear(String(0))
    StringAppend(String(0), filename, 256)
    StringChopRight(String(0), 3)
    StringAppend(String(0), "RGB", 256)
    first_file = FileOpen(String(0), FILE_NORMAL)
    if(first_file)
      WindowMegaImage(19.0,0.0,  22.5,0.0,  22.5,2.5,  19.0,2.5,  0.0,0.0,  1.0,1.0,  WHITE, ALPHA_NONE, NULL)
      WindowImage(19.0, 0.0, 3.5, 2.5, first_file, NULL, 0)



  // Main window
  WindowBorder(FALSE, -18.0, -10.00, 36, 18, BORDER_FULL)


  // Slider
  file_slider = WindowSlider(24.0, 0.0, 1, 18, file_slider)
  first_file = (SystemGet(SYS_FILECOUNT, 0, 0) * file_slider) >> 8



  // Selectable files
  i = 0
  while(i < 18)
    WindowString(WHITE, 0.0,  i, SystemGet(SYS_FILENAME, i+first_file, 0))
    WindowString(WHITE, 13.0, i, SystemGet(SYS_FILESIZE, i+first_file, 0))
    click = WindowImage(0.0, i, 21.0, 1.0, NULL, NULL, 0)
    if(click == 255)
      StringClear(filename)
      StringAppend(filename, SystemGet(SYS_FILENAME, i+first_file, 0), 256)
    if(SystemGet(SYS_FILEFTPFLAG, i+first_file, 0))
      click = WindowImage(22.0, i, 1.0, 1.0, "FILE:WINYABOX.RGB", NULL, 0)
      if(click == 255)
        SystemSet(SYS_FILEFTPFLAG, i+first_file, 0, FALSE)
    else
      click = WindowImage(22.0, i, 1.0, 1.0, "FILE:WINNOBOX.RGB", NULL, 0)
      if(click == 255)
        SystemSet(SYS_FILEFTPFLAG, i+first_file, 0, TRUE)
    i++


  // Button images
  button_one = ButtonImage(button_one, 26.0, 0.0, 10.0, 2.0, "Import")
  button_two = ButtonImage(button_two, 26.0, 2.0, 10.0, 2.0, "Export")
  button_three = ButtonImage(button_three, 26.0, 4.0, 10.0, 2.0, "Delete")
  button_four = ButtonImage(button_four, 26.0, 6.0, 10.0, 2.0, "Update")
  button_five = ButtonImage(button_five, 26.0, 8.0, 10.0, 2.0, "SDF Save")
  button_six = ButtonImage(button_six, 26.0, 10.0, 10.0, 2.0, "Text Edit")
  button_seven = ButtonImage(button_seven, 26.0, 12.0, 10.0, 2.0, "Modeler")
  button_eight = ButtonImage(button_eight, 26.0, 14.0, 10.0, 2.0, "Room Edit")
  button_nine = ButtonImage(button_nine, 26.0, 16.0, 10.0, 2.0, "GoPoof")


//-----------------------------------------------------------------------
