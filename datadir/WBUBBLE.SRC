//-----------------------------------------------------------------------
#define message    self.stat00w
#define frame      self.stat01w
#define angle      self.stat02w

#define GREEN_COLOR  (120<<8)

// Model setup functions use team to determine eye type...
#define team       self.team

//-----------------------------------------------------------------------
Spawn()
  int self, class, openfile
  self = FindSelf()
  message = RollDice(1,28)
  team = TEAM_GOOD
  float screenx

  screenx = SystemGet(SYS_SCREENPARAMS, 0, 2)

  // Setup the talker model...
  self.eqcol01 = RollDice(1, 256)
  self.eqcol23 = RollDice(1, 256)


  // Sometimes armor'd...
  self.eqhelm = ITEM_NONE
  self.eqbody = ITEM_NONE
  self.eqlegs = ITEM_NONE
  self.eqleft = ITEM_NONE
  self.eqright = ITEM_NONE
  if(RollDice(1, 100) < 50)
    self.eqbody = ITEM_BODY_1 + ((RollDice(1, 3)-1)<<2)
    if(RollDice(1, 100) < 50)
      self.eqhelm = ITEM_HELM_1 + ((RollDice(1, 3)-1)<<2)
      self.eqlegs = ITEM_LEGS_1 + ((RollDice(1, 3)-1)<<2)
  if(RollDice(1, 100) < 50)
    if(RollDice(1, 100) < 50)
      self.eqright = ITEM_DAGGER
    else
      self.eqright = ITEM_SHORT_SWORD
  else
    if(RollDice(1, 100) < 50)
      self.eqright = ITEM_MACE
    else
      self.eqright = ITEM_HATCHET
  if(RollDice(1, 100) < 50)
    self.eqright = ITEM_QUIVER_10
    self.eqleft = ITEM_BOW
  else
    if(RollDice(1, 100) < 50)
      self.eqleft = ITEM_SHIELD



  class = RollDice(1, 8)-1
  if(class == CLASS_HEALER)
    self.eqhelm = ITEM_NONE
  // Make minority types occur one fourth of the time...
  if(RollDice(1, 100) < 50)
    self.eqcol01 = self.eqcol01 & 127
  openfile = WSPAWN.ClassSetup(class, FALSE)


  frame = SystemGet(SYS_MODELACTIONSTART, openfile, ACTION_STAND)
  if(frame == 65535)
    frame = 0


  // Put us in the right spot...
  self.x = screenx - 19.0
  self.y = 106.0


  // Drop into place...
  angle = 0

//-----------------------------------------------------------------------
// Takes one long string and spreads it across several smaller ones...
int CutDown(int string_to_cut)
  int length, total_length
  int i, j, current_string, value

  total_length = StringLength(string_to_cut)
  current_string = 0
  length = 0
  i = 0
  while(i < total_length && current_string < 6)
    value = StringGetValue(string_to_cut, i)
    if(value == '_')
      value = 2
    if(value != ' ' || length > 0)
      StringSetValue(String(current_string), length, value)
      length++
    if(length > 30)
      // We filled up this string...  Backtrack to the start of the
      // last word...
      j = 0
      while(j < length && value != ' ')
        value = StringGetValue(String(current_string), length-1-j)
        StringSetValue(String(current_string), length-1-j, 0)
        j++


      // Check for errors...
      if(j == length)
        // Uh, oh...  Somethin' crazy happen'd...
        StringClear(String(0))
        StringAppend(String(0), "CutDown() Error", 256)
        return 1


      // Terminate this string and move on to the next...
      StringSetValue(String(current_string), length, 0)
      current_string++
      length = 0
      i = i-j
    i++


  // Terminate the last string...
  StringSetValue(String(current_string), length, 0)
  return (current_string+1)


//-----------------------------------------------------------------------
Refresh()
  int self, size_x, size_y, y, x, scaling
  float val, ratio
  self = FindSelf()
  ratio = SystemGet(SYS_SCREENPARAMS, 0, 2)
  ratio = ratio / SystemGet(SYS_SCREENPARAMS, 0, 3)

  // Set the scaling to large...
  SystemSet(SYS_WINDOWSCALE, 0, 0, 2048)



  if(angle < 90)
    angle = angle + SystemGet(SYS_MAINFRAMESKIP, 0, 0)
    if(angle > 90)
      angle = 90
  val = 50.0 - (sin(angle)*50.0)



  // Draw the character image...
  WindowBorder(FALSE, -5.0+val, -6.0, 10, 14, BORDER_NONE)
  WindowMegaImage(-40.0,-6.0,    7.0,-6.0,    7.0,28.0,   -40.0,28.0,   0.02,0.02,  0.98,0.98,  GREEN_COLOR,   ALPHA_NONE,  "FILE:GRADIENT.RGB")


  Window3DStart(-7.0 * ratio + 4.0, 0.0, 15.0 * ratio, 16.0, FALSE)
  Window3DPosition(0.0, 0.0, 2.5, WIN_TARGET)
  Window3DPosition(4.0, 4.25, 5.25, WIN_CAMERA)
  Window3DModel(WIN_3D_MODEL, MODEL_BASE_FILE, frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_LEGS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_CHEST_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_ARMS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_PADS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_FEET_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_HEAD_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_HAIR_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_MOUTH_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_EYES_FILE, 0, 255)

  Window3DModel(WIN_3D_MODEL, MODEL_LEFT_FILE, 1, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_RIGHT_FILE, 1, 255)

  Window3DEnd()


  // Figure out how big to make the bubble...
  StringClear(String(10))
  if(message == 1)
    StringAppend(String(10), "\34All animals are equal, but some animals are more equal than others.\34 -George_Orwell", 256)
  if(message == 2)
    StringAppend(String(10), "\34Tricks and treachery are the practice of fools, who don't have the sense to be honest.\34 -Benjamin_Franklin", 256)
  if(message == 3)
    StringAppend(String(10), "\34Those who cast the votes decide nothing.  Those who count the votes decide everything.\34 -Josef_Stalin", 256)
  if(message == 4)
    StringAppend(String(10), "\34During times of universal deceit, telling the truth becomes a revolutionary act.\34 -George_Orwell", 256)
  if(message == 5)
    StringAppend(String(10), "\34I was mentally fit to carry nuclear weapons, but I'm not mentally fit if I see a UFO.\34 -Maj._George_A._Filer,_III", 256)
  if(message == 6)
    StringAppend(String(10), "\34I penned it down, until at last it came to be, for length and breadth, the bigness which you see.\34 -John_Bunyan", 256)
  if(message == 7)
    StringAppend(String(10), "\34We are cups, constantly and quietly being filled.  The trick is, knowing how to tip ourselves over and let ", 256)
    StringAppend(String(10), "the beautiful stuff out.\34 -Ray_Bradbury", 256)
  if(message == 8)
    StringAppend(String(10), "\34Life is like a roll of toilet paper, the closer it gets to the end, the faster it goes.\34 -Andy_Rooney", 256)
  if(message == 9)
    StringAppend(String(10), "\34The best cure for insomnia is to get a lot of sleep.\34 -W._C._Fields", 256)
  if(message == 10)
    StringAppend(String(10), "\34When ugly people make weird faces, strange people win races.\34 -Lerry", 256)
  if(message == 11)
    StringAppend(String(10), "\34Government to be lasting will have to be just.\34 -Ed_Leedskalnin", 256)
  if(message == 12)
    StringAppend(String(10), "\34Men often become what they believe themselves to be.\34 -Mahatma_Gandhi", 256)
  if(message == 13)
    StringAppend(String(10), "\34Being the last man on the Moon is a very dubious honor.\34 -Gene_Cernan", 256)
  if(message == 14)
    StringAppend(String(10), "\34The body says what words cannot.\34 -Martha_Graham", 256)
  if(message == 15)
    StringAppend(String(10), "\34Trust thyself; every heart vibrates to that iron string.\34 -Ralph_Waldo_Emerson", 256)
  if(message == 16)
    StringAppend(String(10), "\34Always do what you're afraid to do.\34 -Ralph_Waldo_Emerson", 256)
  if(message == 17)
    StringAppend(String(10), "\34It is not desirable to cultivate a respect for the law, so much as for the right.\34 -Henry_David_Thoreau", 256)
  if(message == 18)
    StringAppend(String(10), "\34If you had told me fifty years ago that I would build my activities from the proceeds of windbells, ", 256)
    StringAppend(String(10), "I would have said you were crazy.\34 -Paolo_Soleri", 256)
  if(message == 19)
    StringAppend(String(10), "\34We do not inherit the earth from our parents, we borrow it from our children\34 -Kenyan_Proverb", 256)
  if(message == 20)
    StringAppend(String(10), "\34Mark my word: A combination airplane and motorcar is coming. You may smile. But it will come...\34 -Henry_Ford", 256)
  if(message == 21)
    StringAppend(String(10), "\34You cannot depend on your eyes when your imagination is out of focus.\34 -Mark_Twain", 256)
  if(message == 22)
    StringAppend(String(10), "\34Unless we attempt the absurd we cannot achieve the impossible.\34 -Albert_Einstein", 256)
  if(message == 23)
    StringAppend(String(10), "\34Every generation laughs at the old fashions, but religiously follows the new.\34 -Henry_David_Thoreau", 256)
  if(message == 24)
    StringAppend(String(10), "\34Clothes make the man. Naked people have little or no influence on society.\34 -Mark_Twain", 256)
  if(message == 25)
    StringAppend(String(10), "\34Self-education is, I firmly believe, the only kind of education there is.\34 -Isaac_Asimov", 256)
  if(message == 26)
    StringAppend(String(10), "\34The real discipline comes in saying no to the wrong opportunities.\34 -Peter_Drucker", 256)
  if(message == 27)
    StringAppend(String(10), "\34The real gunslingers always tried to avoid pulling their guns.\34 -Michael_Crichton", 256)
  if(message == 28)
    StringAppend(String(10), "\34Live as you will have wished to have lived when you are dying.\34 -Christian_Gellert", 256)


  size_y = CutDown(String(10))
  size_x = 1
  y = 0
  while(y < size_y)
    x = StringLength(String(y))
    if(x > size_x)
      size_x = x
    y++


  // Draw the bubble...  
  WindowBorder(FALSE,-3.0-size_x, -8.0-(size_y*0.50)-val, 2, 2, BORDER_NONE)

  // Black bubble...
  WindowImage(-0.25, -0.25, 2.25, size_y+2.5, KANJI_IMAGE, TRUE, 7)
  WindowImage(1.0, -0.25, size_x, size_y+2.5, KANJI_IMAGE, TRUE, 9)
  WindowImage(size_x, -0.25, 2.25, size_y+2.5, KANJI_IMAGE, TRUE, 8)
  WindowImage(size_x-0.33, size_y+1.8, 1.75, 2.1, KANJI_IMAGE, TRUE, 10)

  // White bubble...
  WindowImage(0.0, 0.0, 2.0, size_y+2, KANJI_IMAGE, FALSE, 7)
  WindowImage(1.0, 0.0, size_x, size_y+2, KANJI_IMAGE, FALSE, 9)
  WindowImage(size_x, 0.0, 2.0, size_y+2, KANJI_IMAGE, FALSE, 8)
  WindowImage(size_x, size_y+1.8, 1.0, 1.2, KANJI_IMAGE, FALSE, 10)



  // Draw the bubble text...
  y = 0
  while(y < size_y)
    WindowString(BLACK, 1.0, y+1, String(y))
    y++



  // Draw the Zucchini thing...
  WindowBorder(FALSE, -30.0 * ratio, 14.0, 0, 0, 0)
  WindowMegaImage(0.0,0.0,  4.0,0.0,  4.0,6.0,  0.0,6.0,  0.0,0.0,  1.0,1.0,  white, 255, "FILE:ZUCCHINI.RGB")



  // Reset the scaling to whatever the user wants...
  scaling = FileReadByte("FILE:CONFIG.DAT", 64)<<8
  SystemSet(SYS_WINDOWSCALE, 0, 0, (scaling&768)<<2)


//-----------------------------------------------------------------------
