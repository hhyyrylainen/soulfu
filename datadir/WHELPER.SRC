#define button_one  self.stat00l
#define title       self.string0
#define gem0        self.stat00h

#define icon_pick   self.stat02l
#define icon_drag   self.stat03l
#define icon_drop   self.stat03h
#define timer       self.stat04w
#define icon_from   self.stat11w

#define joy_mode    self.stat15l  // For ENCHANT.SRC
#define joy_hit     self.stat15h  // For ENCHANT.SRC


#define HITS_RED    (192<<16)
#define MANA_BLUE   (255 | (64<<8))





//-----------------------------------------------------------------------
Spawn()
  int self
  self = FindSelf()
  button_one = 0
  gem0 = FALSE
  StringClear(title)
  StringAppend(title, "Helper", 16)

  icon_pick = 224
  icon_drag = 0
  icon_drop = FALSE
  icon_from = 65535
  timer = 0
  joy_mode = FALSE
  joy_hit = 0

//-----------------------------------------------------------------------
Refresh()
  int    self, binding
  int    length, c
  int    input_return
  float  x
  self = FindSelf()
  binding = FindByIndex(self.binding)
  if(binding)
    // Make sure binding is either a local player, or one of our pets...
    if(!IndexIsLocalPlayer(self.binding) && !IndexIsLocalPlayer(binding.owner))
      BuzzerSound()
      GoPoof(POOF_SELF)


    // Button actions
    if(button_one == 250)
      ButtonSound()
      WSPAWN.UnspaceString(binding.name)
      GoPoof(POOF_SELF)


    // Title bar
    if(StringGetValue(binding.name, 0) == 0)
      TitleBar(TRUE, 0.0, 0.0, title)
    else
      TitleBar(TRUE, 0.0, 0.0, binding.name)







    // Show helper's inventory
    SystemSet(SYS_CURRENTITEM, 0, 0, 200)
    if(gem0)
      WindowBorder(FALSE, -8.5, 2.0, 17, 14, BORDER_LOW_TITLE)


      // Limited 6 item inventory...
      c = 224
      x = -0.5
      while(c < 230)
        WSTATUS.RowIcon(c, x, 8.5)
        x = x+3.0
        c++


      // Weapons
      WindowBorder(FALSE, -8.0, 3.7, 6, 6, BORDER_BOX)
      WSTATUS.RowIcon(242, 0.0, 0.0)
      WSTATUS.RowIcon(243, 3.0, 0.0)

      WSTATUS.RowIcon(244, 0.0, 3.0)
      WSTATUS.RowIcon(245, 3.0, 3.0)



      // Armor & Stats...
      WindowBorder(FALSE, -1.0, 3.7, 9, 6, BORDER_BOX)
      if(binding.sflags & SCRIPT_ALLOW_HELM)
        WSTATUS.RowIcon(246, 0.0, 0.0)
      if(binding.sflags & SCRIPT_ALLOW_BODY)
        WSTATUS.RowIcon(247, 3.0, 0.0)
      if(binding.sflags & SCRIPT_ALLOW_LEGS)
        WSTATUS.RowIcon(248, 6.0, 0.0)


      // Strength
      WindowString(WHITE, 0.0, 3.0, StringLanguage(55))
      StringClear(String(0))
      StringAppendNumber(String(0), binding.str, 256)
      WindowString(WHITE, 9.0-StringLength(String(0)), 3.0, String(0))

      // Dexterity
      WindowString(WHITE, 0.0, 4.0, StringLanguage(56))
      StringClear(String(0))
      StringAppendNumber(String(0), binding.dex, 256)
      WindowString(WHITE, 9.0-StringLength(String(0)), 4.0, String(0))

      // Intelligence
      WindowString(WHITE, 0.0, 5.0, StringLanguage(57))
      StringClear(String(0))
      StringAppendNumber(String(0), binding.int, 256)
      WindowString(WHITE, 9.0-StringLength(String(0)), 5.0, String(0))


      // Show name changer and close button...
      WindowBorder(FALSE, -8.0, 14.0, 16, 3, BORDER_BOX)


      // Name changer...
      SystemSet(SYS_CURRENTITEM, 0, 0, 300)
      WSPAWN.CorrectString(binding.name)
      input_return = WindowInput(0.0, 0.0, 16, binding.name, 1)


      // Buttons...
      button_one = ButtonImage(button_one, 0.0, 1.0, 16.0, 2.0, StringLanguage(239))
      if(input_return == 255)
        button_one = input_return







    // Main window
    SystemSet(SYS_CURRENTITEM, 0, 0, 400)
    WindowBorder(FALSE, -8.5, 0.0, 17, 2, BORDER_BOX)


    // Hits
    WindowString(WHITE, 1.0, 0.0, StringLanguage(50))
    WSTATUS.DrawStatusBar(6.5, 0.0, HITS_RED, binding.hits, binding.hitsmax, binding.hitstap, binding.pstimer)


    // Mana
    if(binding.manamax > 0)
      WindowString(WHITE, 1.0, 1.0, StringLanguage(51))
      WSTATUS.DrawStatusBar(6.5, 1.0, MANA_BLUE, binding.mana, binding.manamax, binding.manatap, FALSE)


    // Show the extended view gem...
    SystemSet(SYS_CURRENTITEM, 0, 0, 500)
    c = 0
    if(gem0)
      c = WindowImage(-0.6, -0.6, 1.0, 1.0, "FILE:=WINGEMG.RGB", NULL, 0)
    else
      c = WindowImage(-0.6, -0.6, 1.0, 1.0, "FILE:=WINGEMR.RGB", NULL, 0)
    if(c == 255) 
      gem0 = (gem0+1)&1
      ButtonSound()
  else
    GoPoof(POOF_SELF)


  // Do the special drag/drop stuff...
  WSTATUS.SpecialDragDropStuff()


  timer = timer + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*900

//-----------------------------------------------------------------------
