//-----------------------------------------------------------------------
SetupControls(int player)
  int self, file, offset, key, button, i, device
  self = FindSelf()
  file = "FILE:CONFIG.DAT"
  offset = 256+(player<<5)

  // Pick the device...
  device = FileReadByte(file, offset+23)
  if(SystemSet(SYS_PLAYERDEVICE, player, PLAYER_DEVICE_TYPE, device)==FALSE)
    // Device setup failed...
    SystemSet(SYS_PLAYERDEVICE, player, PLAYER_DEVICE_TYPE, 0)
    return 0


  // Load keyboard keys...
  if(device == 1)
    i = 0
    while(i < 9)
      key = FileReadByte(file, offset+(i<<1)) << 8
      key = key | FileReadByte(file, offset+(i<<1)+1)
      SystemSet(SYS_PLAYERDEVICE, player, i, key)
      i++


  // Load joystick buttons...
  if(device > 1)
    i = 0
    while(i < 5)
      button = FileReadByte(file, 18+offset+i)
      SystemSet(SYS_PLAYERDEVICE, player, i, button)
      i++
    // Camera buttons stored in different spot...
    while(i < 9)
      button = FileReadByte(file, 19+offset+i)
      SystemSet(SYS_PLAYERDEVICE, player, i, button)
      i++



//-----------------------------------------------------------------------
// This is where the program starts...
Spawn()
  int file
  int i, value


  // Start looped/distance sounds (insect buzz, etc.)
  SystemSet(SYS_SFXVOLUME, 0, 0, 255)
  // Infinite loop == FFFF
  StringSetValue(String(0), 0, 255)
  StringSetValue(String(0), 1, 255)
  // Fade time == 00
  StringSetValue(String(0), 2, 0)
  // Loop begin == 000000
  StringSetValue(String(0), 3, 0)
  StringSetValue(String(0), 4, 0)
  StringSetValue(String(0), 5, 0)
  PlayMegaSound("FILE:=INSECT.RAW", 256, 255, 128, String(0))



  // Load info from the config file
  file = "FILE:CONFIG.DAT"


  // Mip Map Active
  value = FileReadByte(file, 98)
  SystemSet(SYS_MIPMAPACTIVE, 0, 0, value)


  // Water Layers Active
  value = FileReadByte(file, 99)
  SystemSet(SYS_WATERLAYERSACTIVE, 0, 0, value)


  // Cartoon lines
  value = FileReadByte(file, 100)
  SystemSet(SYS_CARTOONMODE, 0, 0, value)


  // Fast and Ugly hardcoded...


  // Flipped speakers
  value = FileReadByte(file, 102)
  SystemSet(SYS_FLIPPAN, 0, 0, value)


  // Turn on control devices for the players, according to
  // last configuration
  SetupControls(0)
  SetupControls(1)
  SetupControls(2)
  SetupControls(3)


  // Other stuff
  SystemSet(SYS_WINDOWSCALE, 0, 0, ((FileReadByte(file, 64)<<8)&768)<<2)
  SystemSet(SYS_SFXVOLUME, 0, 0, FileReadByte(file, 65))
  SystemSet(SYS_MUSICVOLUME, 0, 0, FileReadByte(file, 66))


  // Run some critical setup scripts...
  LANGUAGE.Setup()


  // Pop up the resolution test...
  Spawn(WINDOW, SystemGet(SYS_SCREENPARAMS, 0, 2) * 0.5, 150.0, 1.0, "FILE:WSCREEN.RUN")
  GoPoof(POOF_SELF)


 
//-----------------------------------------------------------------------
