//-----------------------------------------------------------------------
#define message    self.stat00w
#define timer      self.stat01w

//-----------------------------------------------------------------------
Spawn()
  int self
  float screenx
  self = FindSelf()
  screenx = SystemGet(SYS_SCREENPARAMS, 0, 2)

  // Pick a random message
  message = RollDice(1,20)

  // Drop into place...
  timer = 0
  self.x = screenx
  self.y = 300.0

//-----------------------------------------------------------------------
Refresh()
  int self, size
  float offset, screenx
  self = FindSelf()
  screenx = SystemGet(SYS_SCREENPARAMS, 0, 2)

  // Adjust the timer...
  timer = timer + SystemGet(SYS_MAINFRAMESKIP, 0, 0)


  // Poof if over max...  Only last 15 seconds or so...
  if(timer > 500)
    GoPoof(POOF_SELF)


  // Correct our position based on the timer...
  self.x = screenx
  if(timer < 30)
    self.x = screenx + ((30-timer)<<1)
  if(timer > 470)
    self.x = screenx + ((timer-470)<<1)
  self.y = 300.0


  // Make this the topmost window...
  SystemSet(SYS_TOPWINDOW, 0, 0, 0)


  // Figure out how big to make the bubble...
  StringClear(String(0))
  StringAppend(String(0), StringLanguage(309 + message), 256)

  size = StringLength(String(0))


  // Draw the bubble...  
  offset = -size-6.75
  if(timer < 30)
    offset = timer * offset * 0.033333
  if(timer > 470)
    offset = (500-timer) * offset * 0.033333

  WindowBorder(FALSE,offset, -3.0, 2, 2, BORDER_NONE)


  // Leader portion...
  WindowImage(-2.25, 0.25, 2.75, 2.25, KANJI_IMAGE, TRUE, 9)
  WindowImage(-2.0, 0.5, 2.25, 1.75, KANJI_IMAGE, FALSE, 9)


  // Main text portion...
  WindowImage(-0.25, -0.25, size+3, 2.25, KANJI_IMAGE, TRUE, 9)
  WindowImage(0.0, 0.0, size+3, 1.75, KANJI_IMAGE, FALSE, 9)
  WindowString(BLACK, 0.5, 0.5, String(0))


  // Draw the cookie
  WindowBorder(FALSE, -6.0, -6.0, 6, 6, BORDER_NONE)
  WindowImage(0.0, 0.0, 6.0, 6.0, "FILE:=COOKIE.RGB", NULL, 0)


//-----------------------------------------------------------------------
