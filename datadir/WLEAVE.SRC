#define button_one    self.stat02l
#define button_two    self.stat02h

//-----------------------------------------------------------------------
Spawn()
  int self
  self = FindSelf()
  button_one = 0
  button_two = 0


//-----------------------------------------------------------------------
Refresh()
  int self
  int player, binding, child, i, type, num_player, target
  int x, y, z
  float ypos, xpos, xadd
  float screenx

  screenx = SystemGet(SYS_SCREENPARAMS, 0, 2)

  // Allow us to save data in the window's properties...
  self = FindSelf()


  // Button actions
  if(button_one == 250)
    ButtonSound()
    GoPoof(POOF_ALL_OTHER_WINDOWS)
    Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WMAIN.RUN")
    SystemSet(SYS_LEAVEGAME, 0, 0, 0)
    GoPoof(POOF_SELF)
  if(button_two == 250)
    // Spawn the local characters and move them into their
    // starting room...  Convoluted so it functions (?)
    // over the network...
    SystemSet(SYS_LOCALPLAYER, 0, 0, 65535)
    SystemSet(SYS_LOCALPLAYER, 1, 0, 65535)
    SystemSet(SYS_LOCALPLAYER, 2, 0, 65535)
    SystemSet(SYS_LOCALPLAYER, 3, 0, 65535)
    GoPoof(WARN_ALL_OTHER_WINDOWS)
    GoPoof(POOF_ALL_OTHER_WINDOWS)
    SystemSet(SYS_CAMERARESET, 0, 0, 0)
    SystemSet(SYS_STARTGAME, 0, 0, 0)
    SystemSet(SYS_MESSAGERESET, 0, 0, 0)



    // Make a message window...
    Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WMESSAGE.RUN")


    // Count local players...
    num_player = 0
    player = 0
    while(player < 4)
      self.binding = SystemGet(SYS_LOCALPLAYER, player, 0)
      binding = FindBinding()
      if(binding)
        num_player++
      player++


    // Make a status window for each player character...
    if(num_player > 0)
      ypos = 300.0 - (3.0 * ((SystemGet(SYS_WINDOWSCALE, 0, 0) / 600.0) + 6.0))
      xadd = screenx / num_player
      xpos = xadd*0.5
      player = 0
      while(player < 4)
        self.binding = SystemGet(SYS_LOCALPLAYER, player, 0)
        binding = FindBinding()
        if(binding)
          child = Spawn(WINDOW, xpos, ypos, player, "FILE:WSTATUS.RUN")
          xpos = xpos + xadd
        player++


    // Poof each player, so they start out with a "Respawn Ready"
    // (which automatically goes away & spawns 'em)
    player = 0
    while(player < 4)
      self.target = SystemGet(SYS_LOCALPLAYER, player, 0)
      target = FindTarget()
      if(target)
        GoPoof(POOF_TARGET)
      player++
    SystemSet(SYS_CAMERARESET, 0, 0, 0)


    // Make the Gong sound (done here so we don't get multiple gongs
    // for multiple players)
    PlaySound("FILE:-GONG0.RAW", 256, 255)


    // Reset luck...
    SystemSet(SYS_LUCK, 0, 0, 0)


    // Reset the camera
    SystemSet(SYS_CAMERASPIN, 0, 0, 32000)


    // Make a fader window...
    Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WFADER.RUN")


    // Poof this window...
    ButtonSound()
    GoPoof(POOF_SELF)



  // Main window
  WindowBorder(FALSE, -18.0, -1.0, 36, 2, BORDER_FULL)


  // Button images
  button_one = ButtonImage(button_one, 0.0, 0.0, 18.0, 2.0, StringLanguage(233))  // Exit Game
  button_two = ButtonImage(button_two, 18.0, 0.0, 18.0, 2.0, StringLanguage(234))  // Start Game


//-----------------------------------------------------------------------
