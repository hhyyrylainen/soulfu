#define button0  self.stat00l  // Used to close from WMessage...
#define camx     self.stat01w
#define camy     self.stat02w
#define oldcamx  self.stat03w
#define oldcamy  self.stat04w
#define oldx     self.stat05w
#define oldy     self.stat06w
#define oldlevel self.stat07w
#define oldroom  self.stat08w
#define button1  self.stat09l
#define force_update self.stat09h

//-----------------------------------------------------------------------
Spawn()
  int self
  self = FindSelf()
  // Make this the topmost window...
  SystemSet(SYS_TOPWINDOW, 0, 0, 0)
  camx = 32768
  camy = 32768
  oldcamx = 0
  oldcamy = 0
  oldx = 65535
  oldy = 65535
  oldlevel = 65535
  oldroom = 65535
  button0 = 0
  button1 = 0
  force_update = FALSE

//-----------------------------------------------------------------------
Refresh()
  int self, i, temp
  int curx, cury, curroom, curlevel, flags
  int offx, offy
  float cx, cy
  float sx, sy
  float x, y, z
  float tlx, tly, trx, try, brx, bry, blx, bly
  float frustumx, frustumy
  self = FindSelf()


  // Close from WMESSAGE...
  if(button0 == 255)
    ButtonSound()
    GoPoof(POOF_SELF)


  // Title bar
  TitleBar(TRUE, 0.0, 1.5, StringLanguage(6))


  // Main window
  WindowBorder(FALSE,-7.5, 1.5, 15, 15, BORDER_FULL)


  // Camera movement
  sy = SystemGet(SYS_MAPSIDENORMAL, 0, 0)*-0.0035
  sx = SystemGet(SYS_MAPSIDENORMAL, 1, 0)*0.0035
  curx = SystemGet(SYS_CURSORPOS, 0, 0)
  cury = SystemGet(SYS_CURSORPOS, 0, 1)
  if(oldx < 1500 && oldy < 1500)
    if(SystemGet(SYS_TOPWINDOW, 0, 0))
      temp = SystemGet(SYS_CURSORBUTTONDOWN, 0, 0) + SystemGet(SYS_CURSORBUTTONDOWN, 0, 1) + SystemGet(SYS_CURSORBUTTONDOWN, 0, 2)
      if(temp)
        offx = curx - oldx
        offy = cury - oldy
        camx = camx+(offx*sy)+(offy*sx)
        camy = camy-(offx*sx)+(offy*sy)
  oldx = curx
  oldy = cury


  // Find the current room & level...
  curroom = MAPGEN.GetCurrentRoom()
  curlevel = MAPGEN.GetRoomLevel(curroom)


  flags = MAPGEN.GetRoomFlags(curroom)
  if(flags & MAP_ROOM_FLAG_DUAL_LEVEL)
    // Current room is a staircase type of room...
    // Display next level down if players are low enough...
    z = SystemGet(SYS_LOCALPLAYERZ, 0, 0)
    if(z < -1.0)
      curlevel++


  // Figger out where the automap camera is...
  cx = (camx-32768)*2.0
  cy = (camy-32768)*2.0
  if(camx != oldcamx || camy != oldcamy || curlevel != oldlevel || force_update)
    oldcamx = camx
    oldcamy = camy
    oldlevel = curlevel
    SystemSet(SYS_MAPAUTOMAPPRIME, cx, cy, curlevel)
    force_update = FALSE


  // Draw the map
  Window3DStart(0.0, 0.0, 15, 15, FALSE)
  Window3DPosition(0.4, 0.4, 3000.0, WIN_FRUSTUM)
  Window3DPosition(cx, cy, 0.0, WIN_TARGET)
  Window3DPosition(cx-sx, cy-sy, 800.0, WIN_CAMERA)
  SystemSet(SYS_MAPAUTOMAPDRAW, 0, 0, 0)
  frustumx = SystemGet(SYS_SCREENPARAMS, 0, 2) / 1000.0
  frustumy = SystemGet(SYS_SCREENPARAMS, 0, 3) / 1000.0
  Window3DPosition(frustumx, frustumy, 500.0, WIN_FRUSTUM)
  Window3DEnd()


  // Draw the compass
  x = 0.6
  y = 0.6
  tlx = x + (3.0*sy) - (6.0*sx)
  tly = y + (3.0*sx) + (6.0*sy)
  trx = x - (3.0*sy) - (6.0*sx)
  try = y - (3.0*sx) + (6.0*sy)
  brx = x - (3.0*sy) + (6.0*sx)
  bry = y - (3.0*sx) - (6.0*sy)
  blx = x + (3.0*sy) + (6.0*sx)
  bly = y + (3.0*sx) - (6.0*sy)

//  WindowImage(x-0.25, y-0.25, 0.5, 0.5, "FILE:WHITE.RGB", NULL, 255)
//  WindowImage(tlx-0.25, tly-0.25, 0.5, 0.5, "FILE:RED.RGB", NULL, 255)
//  WindowImage(trx-0.25, try-0.25, 0.5, 0.5, "FILE:DGREEN.RGB", NULL, 255)
//  WindowImage(brx-0.25, bry-0.25, 0.5, 0.5, "FILE:BLUE.RGB", NULL, 255)
//  WindowImage(blx-0.25, bly-0.25, 0.5, 0.5, "FILE:YELLOW.RGB", NULL, 255)
  WindowMegaImage(tlx,tly,  trx,try,  brx,bry,  blx,bly,  0.0,0.0,  1.0,1.0,  WHITE, 255, "FILE:=COMPASS.RGB")
  StringClear(String(0))
  StringAppendNumber(String(0), curlevel, 3)
  CenterString(WHITE, x, y-0.4, String(0))
  button1 = WindowImage(x-1.0, y-1.0, 2.0, 2.0, NULL, NULL, 0)
  if(curroom != oldroom || button1 == 255)
    // Autocenter
    camx = MAPGEN.GetRoomX(curroom)*0.5 + 32768.0
    camy = MAPGEN.GetRoomY(curroom)*0.5 + 32768.0
    oldroom = curroom



//-----------------------------------------------------------------------
